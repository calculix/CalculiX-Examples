read values.fbl
read Naht.frd new

# compute the probe locations
valu two 2
valu neg -1
valu NBh / NB two
valu xN - L o1
valu xN - xN NBh
valu lgh / lg two
valu l1h / l1 two
valu yN + y1 l1h
valu yF + y1 l1
valu yF + yF lgh
valu zbot * T1 neg
valu ztop + Gap T2

# select the nodes
seta nodes n all
enq nodes topseam  rec xN yN ztop 1 i
enq nodes topfault rec xN yF ztop 1 i
enq nodes botseam  rec xN yN zbot 1 i
enq nodes botfault rec xN yF zbot 1 i

# create the time history plots
seta probes se topseam topfault botseam botfault
graph probes time NDTEMP T
sys convert -rotate 90 graph_0.ps Refs/history.png

# document the probe positions
rot x
rot c 90
font d 6
zoom 2
text Positions of the probes
plot na probes
hcpy png
sys mv hcpy_1.png Refs/probes.png

# path plot along the seam on the upper side
enq nodes top rec xN _ ztop 0.1 a
ds -0 e 1
graph top +
sys convert -rotate 90 graph_1.ps Refs/pathplot.png

# plot the path
rot y
frame
rot r 20
rot u 20
zoom 3
text Path for temperature profile
plot n top
hcpy png
sys mv hcpy_2.png Refs/path.png
